Value Global_Auth_type (\S+\s*\(\S+\)|\S+)
Value Global_nas_ip (\S+)
Value Global_statistics (\S+)
Value Global_Retransmit (\d+)
Value Global_Timeout (\d+\s*\(\S+\)|\d+)
Value Global_Passkey (\S+\s*\(\S+\)|\S+)
Value Global_Source_Ip (\S+)
Value Address (\S+)
Value Priority (\d+|-)
Value Retransmit (\d+|-)
Value Auth_Port (\d+|-)
Value Auth_type (\S+|-)
Value Passkey (\S+)
Value Timeout (\d+|-)
Value vrf_mgmt (\S+|-)
Value access_requests (\d+)
Value access_accepts (\d+)
Value access_rejects (\d+)
Value timeout_access_reqs (\d+)
Value access_challenges (\d+)
Value bad_authenticators (\d+)
Value invalid_packets (\d+)
Value si (\S+|-)
Value stats_server_ip (\S+)

Start
  ^\s*RADIUS\s+global\s+auth_type\s+${Global_Auth_type}\s*$$ -> CLICK
  ^\s*RADIUS\s+Global\s+Configuration\s*$$ -> KLISH
  ^\s*HOST\s+AUTH-TYPE\s+KEY\s+AUTH-PORT\s+PRIORITY\s+TIMEOUT\s+RTSMT\s+VRF\s*$$ -> KLISH

CLICK
  ^\s*RADIUS\s+global\s+auth_type\s+${Global_Auth_type}\s*$$
  ^\s*RADIUS\s+global\s+nas_ip\s+${Global_nas_ip}\s*$$
  ^\s*RADIUS\s+global\s+statistics\s+${Global_statistics}\s*$$
  ^\s*RADIUS\s+global\s+retransmit\s+${Global_Retransmit}\s*$$
  ^\s*RADIUS\s+global\s+timeout\s+${Global_Timeout}\s*$$
  ^\s*RADIUS\s+global\s+passkey\s+configured\s+${Global_Passkey}\s*$$
  ^\s*RADIUS\s+global\s+source_ip\s+${Global_Source_Ip}\s*$$
  ^\s*RADIUS_SERVER\s+address\s+${Address}\s*$$
  ^\s*priority\s+${Priority}\s*$$
  ^\s*auth_port\s+${Auth_Port}\s*$$
  ^\s*retransmit\s+${Retransmit}\s*$$
  ^\s*auth_type\s+${Auth_type}\s*$$
  ^\s*timeout\s+${Timeout}\s*$$
  ^\s*passkey\s+${Passkey}\s*$$
  ^\s*vrf\s+${vrf_mgmt}\s*$$
  ^\s*Statistics\s+for\s+RADIUS_SERVER\s+address\s+${stats_server_ip}\s*$$
  ^\s*access_requests\s+${access_requests}\s*$$
  ^\s*access_accepts\s+${access_accepts}\s*$$
  ^\s*access_rejects\s+${access_rejects}\s*$$
  ^$$ -> Record

KLISH
  ^\s*source-ip\s*:\s*${Global_Source_Ip}\s*$$
  ^\s*nas-ip-addr\s*:\s*${Global_nas_ip}\s*$$
  ^\s*statistics\s*:\s*${Global_statistics}\s*$$
  ^\s*timeout\s*:\s*${Global_Timeout}\s*$$
  ^\s*auth-type\s*:\s*${Global_Auth_type}\s*$$
  ^\s*key configured\s*:\s*${Global_Passkey}\s*$$
  ^\s*retransmit\s*:\s*${Global_Retransmit}\s*$$ -> Record
  ^\s*${Address}\s+${Auth_type}\s+${Passkey}\s+${Auth_Port}\s+${Priority}\s+${Timeout}\s+${Retransmit}\s+${vrf_mgmt}\s+${si}\s*$$ -> Record
  ^\s*${Address}\s+${Auth_type}\s+${Passkey}\s+${Auth_Port}\s+${Priority}\s+${Timeout}\s+${Retransmit}\s+${vrf_mgmt}\s*$$ -> Record
  ^\s*${Address}\s+${Auth_type}\s+${Passkey}\s+${Auth_Port}\s+${Priority}\s*$$ -> Record
  ^\s*${Address}\s+${Passkey}\s+${Auth_Port}\s+${Priority}\s*$$ -> Record
  ^\s*${stats_server_ip}:\s*$$
  ^\s*access-accepts:\s+${access_accepts}\s*$$
  ^\s*access-rejects:\s+${access_rejects}\s*$$
  ^\s*access-requests:\s+${access_requests}\s*$$
  ^\s*timeout-access-requests:\s+${timeout_access_reqs}\s*$$
  ^\s*access-challenges:\s+${access_challenges}\s*$$
  ^\s*bad-authenticators:\s+${bad_authenticators}\s*$$
  ^\s*invalid-packets:\s+${invalid_packets}\s*$$ -> Record