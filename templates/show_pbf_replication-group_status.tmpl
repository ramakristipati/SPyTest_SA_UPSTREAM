# show pbf replication-group status { interface { Ethernet ID | PortChannel ID | Vlan ID | Ethernet ID.SUBPORT | # PortChannel ID.SUBPORT } | Switch } [ NAME | type { ip | ipv6 } ]
# Ethernet0
#   Replication-group ipv4-test Type ip
#     Status: Active
#     Members:
#       Entry 1 next-hop 10.1.1.1 recursive (Active)
#       Entry 2 next-hop 10.1.1.2 vrf VrfRed non-recursive
#       Entry 3 next-hop 10.1.1.3 (Active) 
Value Filldown interface (\S+)
Value Filldown repgroup_name (\S+)
Value Filldown repg_type (\w+)
Value Filldown status (\w+)
Value entry (\d+)
Value nh_ip (\S+)
Value nh_type (\S+)
Value nh_vrf (\S+)
Value nh_status (\w+)
Value rep_path_ip (\S+)
Value rep_path_l3intf (\S+)
Value rep_path_l2intf (\S+)

Start
  ^\s*${interface}\s*$$
  ^\s*Replication-group\s+${repgroup_name}\s+Type\s+${repg_type}\s*$$
  ^\s*Status:\s+${status}\s*$$ -> Record
  ^\s*Members:.*$$
  ^\s*Entry\s+${entry}\s+next-hop\s+${nh_ip}\s+vrf\s+${nh_vrf}\s+${nh_type}\s+\(${nh_status}\)\s*$$ ->  Record Start
  ^\s*Entry\s+${entry}\s+next-hop\s+${nh_ip}\s+${nh_type}\s+\(${nh_status}\)\s*$$ ->  Record Start
  ^\s*Entry\s+${entry}\s+next-hop\s+${nh_ip}\s+${nh_type}\s*$$ ->  Record Start
  ^\s*Entry\s+${entry}\s+next-hop\s+${nh_ip}\s+\(${nh_status}\)\s*$$ ->  Record Start
  ^\s*Replication Paths:.*$$
  ^\s*NextHop:${rep_path_ip}\s+L3Intf:${rep_path_l3intf}\s+L2Intf:${rep_path_l2intf}\s*$$ -> Record Start

EOF
